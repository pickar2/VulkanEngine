#version 460

//#extension GL_AMD_gpu_shader_int16 : enable
#extension GL_EXT_shader_explicit_arithmetic_types : enable
#extension GL_KHR_shader_subgroup_basic : enable
#extension GL_KHR_shader_subgroup_ballot : enable
#extension GL_KHR_shader_subgroup_arithmetic : enable
#extension GL_KHR_shader_subgroup_shuffle : enable
#extension GL_EXT_control_flow_attributes : enable

#define WORKGROUP_SIZE 32
//#include "../Generated/material_constants.glsl"

layout (local_size_x = WORKGROUP_SIZE, local_size_y = 1, local_size_z = 1) in;

#include "../Default/constants.glsl"
#include "../Default/structs.glsl"

readonly layout(std430, set = 0, binding = 0) buffer dataArray {
	UiElementData data[];
};

readonly layout(set = 1, binding = 0) buffer counters1Buffer {
	int counters1[];
};

layout(set = 1, binding = 1) buffer counters2Buffer {
	int counters2[];
};

readonly layout(set = 1, binding = 2) buffer offsetsBuffer {
	int offsets[];
};

readonly layout(set = 1, binding = 3) buffer count_const {
	int count;
	int z_count;
};

writeonly layout(std430, set = 2, binding = 0) buffer indicesArray {
	int indices[];
};

#include "../Default/functions.glsl"

void main() {
	if (gl_GlobalInvocationID.x >= count) return;

	UiElementData d = data[gl_GlobalInvocationID.x];

	if ((d.flags & DISABLED) != ZERO || (d.flags & OFFSCREEN) != ZERO) {
		return;
	}
	Pos pos = calcFullPos(d);

	int id4 = int(gl_GlobalInvocationID.x * 4);
	int z = int(pos.z);

	uvec4 activeInvocations = subgroupBallot(true);

	uint firstInvocationWithSameZ = gl_SubgroupInvocationID;
	uint totalAmountOfInvocationsWithZ = 0;
	int offsetAtSubgroupStart;
	int currentInvocationOffset = 0;
	for (uint i = 0; i < gl_SubgroupSize; i++) {
//		if (!subgroupBallotBitExtract(activeInvocations, i)) continue;

		int zOfInvocationI = subgroupShuffle(z, i);
		uvec4 invocationsWithSameZ = subgroupBallot(z == zOfInvocationI);

		if (i == gl_SubgroupInvocationID) {
			firstInvocationWithSameZ = subgroupBallotFindLSB(invocationsWithSameZ);
			totalAmountOfInvocationsWithZ = subgroupBallotBitCount(invocationsWithSameZ);
			uint amountOfPreviousInvocationsWithSameZ = subgroupBallotExclusiveBitCount(invocationsWithSameZ);
			currentInvocationOffset = int(amountOfPreviousInvocationsWithSameZ) * 6;
		}
	}
	if (gl_SubgroupInvocationID == firstInvocationWithSameZ) {
		offsetAtSubgroupStart = atomicAdd(counters2[z], int(totalAmountOfInvocationsWithZ) * 6);
	}
	offsetAtSubgroupStart = subgroupShuffle(offsetAtSubgroupStart, firstInvocationWithSameZ);

	int index = offsets[z] * 6 + offsetAtSubgroupStart + currentInvocationOffset;

	indices[index] = 0 + id4;
	indices[index+1] = 1 + id4;
	indices[index+2] = 2 + id4;
	indices[index+3] = 3 + id4;
	indices[index+4] = 2 + id4;
	indices[index+5] = 1 + id4;
}
